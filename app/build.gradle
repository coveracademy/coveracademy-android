apply plugin: 'com.android.application'

android {
  compileSdkVersion 23
  buildToolsVersion '23.0.1'

  defaultConfig {
    applicationId 'com.coveracademy'
    minSdkVersion 15
    targetSdkVersion 23
    versionCode 1
    versionName '1.0'
    multiDexEnabled true
  }
  signingConfigs {
    release {
      String keyDirectory = "${System.env.KEYSTORES_DIR}"
      File secureProperties = file(keyDirectory.concat("secure.properties"))
      if(secureProperties.exists()) {
        Properties properties = new Properties()
        properties.load(new FileInputStream(secureProperties))
        storeFile file(keyDirectory.concat(properties.getProperty('signing.storeFilePath')))
        storePassword properties.getProperty('signing.storePassword')
        keyAlias properties.getProperty('signing.keyAlias')
        keyPassword properties.getProperty('signing.keyPassword')
      }
    }
    debug {
      storeFile rootProject.file('keystores/debug.keystore')
      keyAlias 'androiddebugkey'
      keyPassword 'android'
      storePassword 'android'
    }
  }
  buildTypes {
    release {
      resValue "string", "google_api_key", System.properties['GOOGLE_API_RELEASE_KEY']
      minifyEnabled false
      shrinkResources false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
    debug {
      resValue "string", "google_api_key", System.properties['GOOGLE_API_DEBUG_KEY']
      minifyEnabled false
      shrinkResources false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }
  sourceSets {
    androidTest {
      java.srcDirs = ['src/test/java']
      resources.srcDirs = ['src/test/resources']
    }
  }
  dexOptions {
    javaMaxHeapSize "4g"
  }
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_7
    targetCompatibility JavaVersion.VERSION_1_7
  }
  packagingOptions {
    exclude 'LICENSE.txt'
    exclude 'META-INF/LICENSE'
    exclude 'META-INF/LICENSE.txt'
    exclude 'META-INF/INDEX.LIST'
    exclude 'META-INF/NOTICE'
    exclude 'META-INF/NOTICE.txt'
  }
}
repositories {
  mavenCentral()
}
dependencies {
  compile project(':api')
  compile 'com.android.support:multidex:1.0.1'
  compile 'com.android.support:appcompat-v7:23.1.0'
  compile 'com.android.support:support-v4:23.1.0'
  compile 'com.android.support:design:23.1.0'
  compile 'com.android.support:recyclerview-v7:23.1.0'
  compile 'com.android.support:cardview-v7:23.1.0'
  compile 'com.android.support:gridlayout-v7:23.1.0'
  compile 'com.google.android.gms:play-services:8.1.0'
  compile 'com.jakewharton:butterknife:7.0.1'
  compile 'com.squareup.picasso:picasso:2.5.2'
  compile 'de.hdodenhof:circleimageview:2.0.0'
  compile 'com.github.rey5137:material:1.2.1'
  compile 'com.facebook.android:facebook-android-sdk:4.7.0'
  compile 'com.google.apis:google-api-services-youtube:v3-rev150-1.20.0'
  compile files('libs/YouTubeAndroidPlayerApi.jar')
}
